&ACCESS RVP
&REL 3
&PARAM SensorITMASK = *
&PARAM TEMPLATE = C:\KRC\Roboter\Template\vorgabe
&PARAM DISKPATH = KRC:\R1\Program
DEF ethernetkrl_workshop( )
;FOLD Declaration
  DECL EKI_STATUS RET
;ENDFOLD (Declaration)


HALT
;initiate and open server
RET=EKI_Init("ITECH")
RET=EKI_Open("ITECH")
HALT

; wait until server is conntected
wait for $FLAG[1]
RET = EKI_ClearBuffer("ITECH", "Sensor")
RET = EKI_ClearBuffer("ITECH", "Robot")

; Main loop
WHILE working

    relFrame = $NULLFRAME
    ; wait to recive data
    wait for $FLAG[4]
    $FLAG[4] = FALSE


    RET= EKI_SetBool("ITECH", "Robot/DataRecived", TRUE)
    RET=EKI_Send("ITECH","Robot/DataRecived")

    RET = EKI_GetString("ITECH","Sensor/Action",action[])

    ;FOLD ACTIONS
        IF (action[1] == "A") THEN
            relFrame.Y = -step_size
            PTP_REL relFrame
        ENDIF
        IF (action[1] == "D") THEN
            relFrame.Y = step_size
            PTP_REL relFrame
        ENDIF
        IF (action[1] == "X") THEN
            relFrame.X = -step_size
            PTP_REL relFrame
        ENDIF
        IF (action[1] == "W") THEN
            relFrame.X = step_size
            PTP_REL relFrame
        ENDIF
        IF (action[1] == "J") THEN
            relFrame.Z = -step_size
            PTP_REL relFrame
        ENDIF
        IF (action[1] == "U") THEN
            relFrame.Z = step_size
            PTP_REL relFrame
        ENDIF
        IF (action[1] == "Q") THEN
            working = FALSE
        ENDIF
    ;ENDFOLD ACTIONS

    wait for $FLAG[4]
    $FLAG[4]=FALSE
    RET= EKI_SetBool("ITECH", "Robot/TaskDone", TRUE)
    current_position = $POS_ACT
    RET= EKI_SetFrame("ITECH","Robot/Data/ActPos",current_position)
    RET=EKI_Send("ITECH","Robot")
    RET = EKI_ClearBuffer("ITECH", "Sensor")
    RET = EKI_ClearBuffer("ITECH", "Robot")


ENDWHILE


RET=EKI_Clear("ITECH")

END





