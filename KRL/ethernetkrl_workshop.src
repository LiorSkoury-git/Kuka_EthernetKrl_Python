&ACCESS RVP
&REL 3
&PARAM SensorITMASK = *
&PARAM TEMPLATE = C:\KRC\Roboter\Template\vorgabe
&PARAM DISKPATH = KRC:\R1\Program
DEF ethernetkrl_workshop( )
;FOLD Declaration
  DECL EKI_STATUS RET
  DECL E6POS firstMove
;ENDFOLD (Declaration)


;FOLD INIT VALUES
    BAS (#INITMOV,0 )
    firstMove = $POS_ACT
    working = TRUE
    action = -1
    key[1] = 0
    current_position = {X 0, Y 0, Z 0, A 0, B 0, C 0}
    temp_frame = {X 0, Y 0, Z 0, A 0, B 0, C 0}
;ENDFOLD INIT VALUES

PTP firstMove

HALT
;initiate and open server
RET=EKI_Init("ITECH")
RET=EKI_Open("ITECH")
HALT

; wait until server is conntected
wait for $FLAG[1]
RET = EKI_ClearBuffer("ITECH", "Sensor")
RET = EKI_ClearBuffer("ITECH", "Robot")

; Main loop
WHILE working

    relFrame = $NULLFRAME
    ; wait to recive data
    wait for $FLAG[4]
    $FLAG[4] = FALSE


    RET= EKI_SetBool("ITECH", "Robot/DataRecived", TRUE)
    RET=EKI_Send("ITECH","Robot/DataRecived")

    RET = EKI_GetInt("ITECH","Sensor/Action",action)

    ;FOLD ACTIONS

    IF (action==0) THEN
        RET = EKI_GetString("ITECH","Sensor/Key",key[])
        ;FOLD KEY OPTIONS
            IF (key[1] == "A") THEN
                relFrame.Y = -step_size
                PTP_REL relFrame
            ENDIF
            IF (key[1] == "D") THEN
                relFrame.Y = step_size
                PTP_REL relFrame
            ENDIF
            IF (key[1] == "X") THEN
                relFrame.X = -step_size
                PTP_REL relFrame
            ENDIF
            IF (key[1] == "W") THEN
                relFrame.X = step_size
                PTP_REL relFrame
            ENDIF
            IF (key[1] == "J") THEN
                relFrame.Z = -step_size
                PTP_REL relFrame
            ENDIF
            IF (key[1] == "U") THEN
                relFrame.Z = step_size
                PTP_REL relFrame
            ENDIF
            IF (key[1] == "Q") THEN
                working = FALSE
            ENDIF
        ;ENDFOLD KEY OPTIONS
    ENDIF


    IF (action == 1) THEN
        RET = EKI_GetFrame("ITECH","Sensor/Frame",temp_frame)
        PTP temp_frame
    ENDIF
    ;ENDFOLD ACTIONS

    wait for $FLAG[4]
    $FLAG[4]=FALSE
    action = -1
    RET= EKI_SetBool("ITECH", "Robot/TaskDone", TRUE)
    current_position = $POS_ACT
    RET= EKI_SetFrame("ITECH","Robot/Data/ActPos",current_position)
    RET=EKI_Send("ITECH","Robot")
    RET = EKI_ClearBuffer("ITECH", "Sensor")
    RET = EKI_ClearBuffer("ITECH", "Robot")


ENDWHILE


RET=EKI_Clear("ITECH")

END





